/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 *
 */

plugins {
  id 'java'
  id("org.kie.kogito.gradle") version "${kogitoVersion}"
  id "org.springframework.boot" version "${springBootVersion}"
  id("io.spring.dependency-management") version "${springBootDependencyManagementVersion}"

  // Inspection purpose
  id "com.dorongold.task-tree" version "${taskTreeVersion}"
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  implementation platform("org.kie.kogito:kogito-spring-boot-bom:${kogitoVersion}")
  implementation("org.springframework.boot:spring-boot-starter-web")
  implementation("org.drools:drools-decisions-spring-boot-starter")
  testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
  testImplementation("org.springframework.boot:spring-boot-starter-test")
  testImplementation("io.rest-assured:rest-assured")
  testImplementation("org.kie.kogito:kogito-scenario-simulation")
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

group 'org.kie.kogito.examples'
version '999-SNAPSHOT'

generateModelConfig {
  jsonSchemaVersion = "DRAFT_2019_09"
}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

springBoot {
  mainClass = 'org.kie.kogito.dmn.springboot.example.KogitoSpringbootApplication'
}


tasks.named("resolveMainClassName") {
  dependsOn(tasks.named("compileSecondaryJava"))
}

tasks.named("bootRun") {
  dependsOn(tasks.named("compileSecondaryJava"))
}

tasks.named("bootJar") {
  dependsOn(tasks.named("compileSecondaryJava"))
}

tasks.named("jar") {
  dependsOn(tasks.named("compileSecondaryJava"))
  from(tasks.named("compileSecondaryJava").get())
}

tasks.named("compileTestJava") {
  dependsOn(tasks.named("compileSecondaryJava"))
}

test {
  dependsOn(tasks.named("compileSecondaryJava"))
  useJUnitPlatform()
}