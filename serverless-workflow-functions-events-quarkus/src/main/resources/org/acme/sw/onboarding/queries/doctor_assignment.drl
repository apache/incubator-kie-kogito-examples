package org.acme.sw.onboarding.queries;
unit AssignmentUnit;

import org.acme.sw.onboarding.model.Doctor;
import org.acme.sw.onboarding.model.Patient;
import org.acme.sw.onboarding.model.SymptomSpecialty;
import org.acme.sw.onboarding.helpers.CalendarHelper;
import java.util.Collections;

rule PatientIsAChild no-loop
    salience 1
when
    $p: /patients[ CalendarHelper.calculateAge(dateOfBirth) < 18 ]
    $d: /doctors[ specialty == "Pediatrics" ]
then
    modify($p) { setAssignedDoctor($d) }
end

rule SymptomHasAMatch no-loop
    salience 2
when
    $p: /patients[ !symptoms.isEmpty() ]
    $s: /symptomSpecialties[ !Collections.disjoint(symptoms, $p.symptoms) ]
    $d: /doctors[ specialty == $s.specialty ]
then
    modify($p) { setAssignedDoctor($d) }
end

rule SymptomNotFound no-loop
    salience 3
when
    $p: /patients
    $d: /doctors[ specialty == "Family medicine" ]
then
    modify($p) { setAssignedDoctor($d) }
end

query AssignDoctor
   $patients : /patients[ assignedDoctor != null  ]
end